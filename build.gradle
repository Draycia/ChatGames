buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

apply plugin: 'java'

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

apply plugin: "com.github.johnrengelman.shadow"

shadowJar{
    minimize()
    relocate 'ninja.leaping.configurate', 'lv.mtm123.chatgames.ninja.leaping.configurate'
    relocate 'co.aikar.commands', 'lv.mtm123.chatgames.acf'
    relocate 'co.aikar.idb', 'lv.mtm123.chatgames.idb'
}

build.dependsOn shadowJar

compileJava{
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'aikar'
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.5'
    compile 'net.kyori:adventure-api:4.0.0-SNAPSHOT'
    compile 'net.kyori:text-adapter-bukkit:3.0.3'
    compile 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    compile 'net.kyori:adventure-text-minimessage:3.0.0-SNAPSHOT'
    compile 'org.spongepowered:configurate-yaml:3.7'
    compile "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"
    compile "com.zaxxer:HikariCP:2.4.1"
    compile "co.aikar:idb-bukkit:1.0.0-SNAPSHOT"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
